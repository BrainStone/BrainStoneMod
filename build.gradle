import static groovyx.net.http.ContentType.*
import static groovyx.net.http.Method.*

import groovy.json.*
import groovyx.net.http.HTTPBuilder
import io.franzbecker.gradle.lombok.task.DelombokTask

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.8"
    id "io.franzbecker.gradle-lombok" version "1.8"
}
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'net.minecraftforge.gradle.forge'

def getChangelog() {
    try {
        def tag = "git describe --abbrev=0 --tags".execute().text.trim();
        return ("git tag -l -n99999 " + tag).execute().text.trim().replaceFirst(tag, "").replaceAll("(?m)^\\s+", "")
    } catch(Exception e) {
        return ''
    }
}
def getVersionType() {
    def lower_version = config.mod_version.toLowerCase();
    
    if (lower_version.contains("prerelease"))
        return "beta";
    else if (lower_version.contains("release"))
        return "release";
    else if (lower_version.contains("dev"))
        return "beta";
    else
        return "alpha";
}
def getVersionName() {
    try {
        def tmp = "git describe --tags --dirty=-SNAPSHOT".execute().text.trim();
        
        if (config.mod_version_prefix.isEmpty())
            return tmp;
        else
            return tmp + " " + config.mod_version_prefix;
    } catch(Exception e) {
      return 'UNKNOWN-VERSION'
    }
}
def signJar(archivePath) {
    if (project.hasProperty('keyStoreAlias') && project.hasProperty('keyStore') && project.hasProperty('keyStorePass')) {
        ant.signjar(
            jar:                  archivePath,
            alias:                project.keyStoreAlias,
            keystore:             project.keyStore,
            storepass:            project.keyStorePass,
            preservelastmodified: true
        )
    } else {
        println 'WARNING!!!\tCannot sign jar!'
    }
}

ext.configFile = file "build.properties"
configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
    
    config.mod_version = getVersionName()
}

def MainDirResources = fileTree(dir: '.', includes: ['README.md', 'LICENSE', 'CHANGELOG.md'])
version = config.mod_version
group = "brainstonemod" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "BrainStoneMod"

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "eclipse"
    replace '${version}', project.version
    mappings = "snapshot_20160518"
    useDepAts = true
}

repositories{
    maven { // JEI, Natura, Mantle, TiCon
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { // EnderIO, EnderCore, EnderZoo, The One Probe
        url = "http://maven.tterrag.com"
    }
    maven { // Draconic Evolution
        url "http://adel.podgeweb.com:1616/maven/"
    }
    maven { // Tesla (dependency of EnderIO)
        url = "http://maven.epoxide.xyz"
    }
    maven { // ChickenBones' mods (dependency of Draconic Evolution)
        url "http://chickenbones.net/maven/"
    }
}

dependencies {
    deobfProvided group: "mezz.jei", name: "jei_1.10.2", version: "3.13.0.335"
    deobfProvided group: "slimeknights", name: "TConstruct", version: "1.10.2-2.5.6b.jenkins425"
    deobfProvided group: "slimeknights.mantle", name: "Mantle", version: "1.10.2-1.1.1.194"
    provided "com.enderio.core:EnderCore:1.10.2-0.4.1.58-beta"
    provided "com.enderio:EnderIO:1.10.2-3.0.1.125_beta"
    provided("com.brandon3055.draconicevolution:Draconic-Evolution:1.10.2-2.0.1.126:deobf") {
        exclude module: 'jei_1.10.2'
        exclude module: 'OpenComputers'
    }
}

lombok {
    version = "1.16.10"
    sha256 = "7e9079406585c67fe25f607c34b17fbed48da0a9bceb15c09a558444cefcef0e"
}

task delombok(type: DelombokTask) {
    args("src/main/java", "-d", "build/sources/delomboked/java")
}

sourceMainJava {
    source = sourceSets.main.allSource
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
    from MainDirResources
}

task showVersion {
    description "Displays the current version"
    group "help"
    
    compileJava.dependsOn showVersion
  
    doLast {
        println version
        
        def versionFile = new File(buildDir, '.version');
    
        versionFile.getParentFile().mkdirs();
        versionFile.text = version;
    }
    
    outputs.upToDateWhen { false }
}

jar {
    doLast {
        if (!gradle.taskGraph.hasTask(reobfJar)) {
            signJar(jar)
        }
    }
}

reobfJar {
    doLast {
        signJar(jar)
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    
    doLast {
        signJar(deobfJar.archivePath)
    }
}

sourceJar {
    doLast {
        signJar(sourceJar.archivePath)
    }
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

curseforge {
    apiKey = project.hasProperty('keyStoreAlias')? project.curseForgeApiKey : 'empty'
    
    project {
        id = '250836'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = getVersionType()
        addGameVersion '1.10.2'
        addGameVersion '1.10'
        addGameVersion '1.9.4'
        addGameVersion 'Java 8'
        
        mainArtifact(jar) {
            displayName = config.mod_version
        }
        addArtifact deobfJar
        addArtifact sourceJar
        
        relations {
            optionalLibrary 'baubles'
            optionalLibrary 'draconic-evolution'
            optionalLibrary 'ender-io'
            optionalLibrary 'overlord'
            optionalLibrary 'just-enough-items-jei'
            optionalLibrary 'tinkers-construct'
            
            //optionalLibrary 'minefactory-reloaded'
            //optionalLibrary 'thermal-foundation'
            //optionalLibrary 'thaumcraft'
        }
    }
}

task updateDownloadServer {
    dependsOn jar
    dependsOn deobfJar
    dependsOn sourceJar
    
    doLast {
        if (!project.hasProperty('secret')) {
            println "Secret needs to be set with the parameter -Psecret=xxx"
            
            return
        }
        
        def http = new HTTPBuilder('http://download.brainstonemod.com/')
        http.request( POST ) {
            uri.path = '/admin/new_version.php'
            requestContentType = URLENC
            body = [
                secret: project.secret,
                version: config.mod_version,
                mc_verion: config.minecraft_version,
                changelog: getChangelog(),
                artifacts: JsonOutput.toJson([
                    universal: jar.archivePath.name,
                    (deobfJar.classifier): deobfJar.archivePath.name,
                    (sourceJar.classifier): sourceJar.archivePath.name
                ]),
                "artifact-universal": jar.archivePath.bytes.encodeBase64().toString(),
                ("artifact-" + deobfJar.classifier): deobfJar.archivePath.bytes.encodeBase64().toString(),
                ("artifact-" + sourceJar.classifier): sourceJar.archivePath.bytes.encodeBase64().toString()
            ]
        
            response.success = { resp, reader ->
                assert resp.status == 200
                println "POST response status: ${resp.statusLine}"
            }
        }
    }
}
